#!/bin/sh
curl -s https://robservatory.com/a-full-history-of-macos-os-x-release-dates-and-rates/ | \
awk -F '[<>]' -v RS='<' '
BEGIN {
	m["Jan"] = 1
	m["Feb"] = 2
	m["Mar"] = 3
	m["Apr"] = 4
	m["May"] = 5
	m["Jun"] = 6
	m["Jul"] = 7
	m["Aug"] = 8
	m["Sep"] = 9
	m["Oct"] = 10
	m["Nov"] = 11
	m["Dec"] = 12
}
/^tr/ {
	i = 0
	for (j=1; spans[j]; j++) {
		++i
		--spans[j]
	}
}
/^td/ {
	++i
	cell = 1
	if (match($0, /rowspan=".*"/))
		spans[i] = substr($0, RSTART+9, RLENGTH-10) - 1
	r[i] = ""
}
/^\/td/ {
	cell = 0
	if (i == 6) {
		split(r[2], date, " ")
		gsub(/[[:space:]]/, "", r[4])
		version = r[4]
		year = r[1] + 0
		month = m[date[1]]
		day = date[2] + 0
		output[++n] = sprintf( \
			"%s\t%04d-%02d-%02d", version, year, month, day \
		)
	}
}
cell { r[i] = r[i] $2 }
END {
	while (--n)
		print output[n]
}
'
